ch21
=========================================================================

# 21. 트리의 구현과 순회

## 21.1 도입
* 계층적 구조를 갖는 자료들을 표현하기 위한 자료구조. 트리(tree)
* 탐색형 자로구조로도 유용하게 쓰임.

### 기초적인 정의와 용어
* 트리(tree) : 노드(node)들과 간선(edge)로 구성되어있다.
* 부모(parent) : 연결된 노드 중 상위 노드.
* 자식(child) : 연결된 노드 중 하위 노드.
* 형제(sibling) : 부모 노드가 같은 노드들.
* 선조(ancestor) : 부모 노드와 그의 부모들.
* 자손(descendant) : 자식 노드와 그 자식들.
* 루트(root) : 모든 노드들을 자손으로 갖는 노드.
* 리프(leaf) : 자식이 하나도 없는 노드.

### 트리와 노드의 속성
* 깊이(depth) : 로트에서 어떤 노드에 도달하기 위해 거쳐야 하는 간선의 수.
* 높이(height) : 트리에서 가장 깊숙히 있는 노드들의 깊이.

### 트리의 재귀적 속성

## 21.2 트리의 순회
```clojure
(ns ch22.core)

(def ex-tree-1
  '{:v 27
    :l {:v 16
        :l {:v 9
            :l nil
            :r {:v 12
                :l nil
                :r nil}}
        :r nil}
    :r {:v 54
        :l {:v 36
            :l nil
            :r nil}
        :r {:v 72
            :l nil
            :r nil}}})


(defn make-node [v l r]
  {:v v :l l :r r})

;; (make-node 1 2 3) ; => {:v 1, :l 2, :r 3}

(defn get-v [node] (get node :v))
(defn get-l [node] (get node :l))
(defn get-r [node] (get node :r))

(defn get-preorder [node]
  (when node
    (concat (list (get-v node))
            (get-preorder (get-l node))
            (get-preorder (get-r node)))))

(get-preorder ex-tree-1) ; => (27 16 9 12 54 36 72)

(defn get-inorder [node]
  (when node
    (concat (get-inorder (get-l node))
            (list (get-v node))
            (get-inorder (get-r node)))))

(get-inorder ex-tree-1) ; => (9 12 16 27 36 54 72)

(defn get-postorder [node]
  (when node
    (concat (get-postorder (get-l node))
            (get-postorder (get-r node))
            (list (get-v node)))))

(get-postorder ex-tree-1) ; => (12 9 16 36 72 54 27)


(defn get-height [node]
  (if node
    (inc (max (get-height (get-l node))
              (get-height (get-r node))))
    -1))

;; (get-height ex-tree-1) ; => 3
```
=========================================================================

## 21.4 트리 순회 순서 변경.

```clojure
;; ID: TRAVERSAL
(def a-pre [27 12 16 9 54 36 72])
(def a-in [9 12 16 27 36 54 72])

(defn get-postorder-from [pre-order in-order]
  (let [N (count pre-order)]
    (when (pos? N)
      (let [root (first pre-order)
            L (.indexOf in-order root) ; value로 node를 찾다니.. 매직이다. value중복되면 난리나겠는걸.
            R (- N 1 L)]
        (concat (get-postorder-from (take L (rest pre-order))
                                    (take L in-order))
                (get-postorder-from (take (dec N) (nthrest pre-order (inc L)))
                                    (take (dec N) (nthrest in-order  (inc L))))
                (list root))))))

;; (get-postorder-from a-pre a-in) ; => (12 9 16 36 72 54 27)

```

# 느낀점.
http://rosettacode.org/wiki/Tree_traversal#Clojure
이거 보니까-_- 왜 이런 생각을 못했지.